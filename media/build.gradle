plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.ajoberstar.grgit' version '4.1.1'
}

def lastCommitHash = ""
if(grgit != null) {
    lastCommitHash = grgit.head().abbreviatedId
}


android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "COMMIT_HASH", "\"${lastCommitHash}\"")

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        prefab true
    }
    ndkVersion "25.0.8775105"
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId 'com.github.a2va'
                artifactId 'media'
                def ver = 'latest'
                /*if (!project.hasProperty('release')) {
                    ver += '-SNAPSHOT'
                }*/
                version project.hasProperty('release') ? '0.1.0' : 'latest'

                from components.release

                pom {
                    name = artifactId
                    description = 'A library for playing media files'
                    url = 'https://github.com/A2va/Media'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    properties = ["commit_hash": lastCommitHash]
                    scm {
                        connection = 'scm:git:git://github.com/A2va/Media.git'
                        developerConnection = 'scm:git:ssh://github.com:A2va/Media.git'
                        url = 'https://github.com/A2va/Media'
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://packagecloud.io/A2va/android/java/maven2/"
                credentials {
                    username localProperties['username']
                    password ''
                }
            }
        }
    }
}

dependencies {

    implementation 'com.google.oboe:oboe:1.6.1'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.core:core:1.5.0'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}